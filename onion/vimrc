se nocompatible number nobomb wrap wrapmargin=80 linebreak textwidth=80
se colorcolumn=80 autoindent history=50 ruler scrolloff=3 nopaste showcmd
se wildmenu notimeout nottimeout display=truncate mouse-=a bg=dark
se conceallevel=1 hlsearch incsearch showmatch ignorecase smartcase
se tabpagemax=15 backspace=indent,eol,start noexpandtab
se langmap=ЙQ,ЦW,УE,КR,ЕT,НY,ГU,ШI,ЩO,ЗP,Х{,Ъ},ФA,ЫS,ВD,АF,ПG,РH,ОJ,ЛK,ДL,ЯZ
se langmap+=ЧX,СC,МV,ИB,ТN,ЬM,Б<,Ю>,йq,цw,уe,кr,еt,нy,гu,шi,щo,зp,х[,ъ],фa,ыs
se langmap+=вd,аf,пg,рh,оj,лk,дl,яz,чx,сc,мv,иb,тn,ьm
noremap <Up> <Nop>
noremap <Down> <Nop>
noremap <Left> <Nop>
noremap <Right> <Nop>
vnoremap <Up> <Nop>
vnoremap <Down> <Nop>
vnoremap <Left> <Nop>
vnoremap <Right> <Nop>
inoremap <Up> <Nop>
inoremap <Down> <Nop>
inoremap <Left> <Nop>
inoremap <Right> <Nop>
map <F1> :w<CR>
map <F2> :wq<CR>
map <F3> :NERDTree<CR>
map <F7> :tabp<CR>
map <F8> :tabn<CR>
map <F9> :tabnew<CR>
call plug#begin()
Plug 'morhetz/gruvbox'
Plug 'lervag/vimtex'
" Plug 'sirver/ultisnips'
Plug 'honza/vim-snippets'
Plug 'preservim/nerdtree'
Plug 'tpope/vim-fugitive'
call plug#end()
" PlugUpdate
" PlugInstall
colo gruvbox
let g:gruvbox_contrast_dark = 'hard'
let g:tex_flavor = 'latex'
let g:vimtex_view_method = 'zathura'
let g:vimtex_quickfix_mode = 0
let g:tex_conceal = 'abdmg'
let g:UltiSnipsExpandTrigger = '<tab>'
let g:UltiSnipsJumpForwardTrigger = '<tab>'
let g:UltiSnipsJumpBackwardTrigger = '<s-tab>'
filetype detect
if &filetype == "python" || &filetype == "vim" || &filetype == "sh"
	se tabstop=4 shiftwidth=4
elseif &filetype == "html" || &filetype == "css"
	se tabstop=2 shiftwidth=2 nolinebreak colorcolumn=140 textwidth=140
elseif &filetype == "asm"
	se tabstop=8 shiftwidth=8 expandtab nolinebreak
elseif &filetype == "go"
	se nolinebreak
elseif &filetype == "haskell"
	se colorcolumn=75 textwidth=75 nolinebreak tabstop=2 shiftwidth=2 expandtab
endif

function PrepareBeforeWrite()
	if &filetype == "python"
		%s/^	/    /e
	endif
	%s/\s\+$//e
	%s/\^datetime\^/\=strftime("%c")/e
	se fenc=utf-8
endfunction


" return '[\s]' if trailing whitespace is detected || return ''
function StatuslineTrailingSpaceWarning()
	if !exists("b:statusline_trailing_space_warning")
		if !&modifiable
			let b:statusline_trailing_space_warning = ''
			return b:statusline_trailing_space_warning
		endif
		if search('\s\+$', 'nw') != 0
			let b:statusline_trailing_space_warning = '[\s]'
		else
			let b:statusline_trailing_space_warning = ''
		endif
	endif
	return b:statusline_trailing_space_warning
endfunction

" return syntax highlight group under cursor || return ''
function StatuslineCurrentHighlight()
	let name = synIDattr(synID(line('.'),col('.'), 1), 'name')
	if name == '' | return ''  | else | return '[' . name . ']' | endif
endfunction

" return a warning for "long lines" where "long" is either textwidth value if it
" was set, otherwise 80
" return '' if no long lines
" return '[#x,my,$z]' if long lines are found, were x is number of long
" lines, y is median length of long lines and z is length of longest line
function StatuslineLongLineWarning()
	if !exists("b:statusline_long_line_warning")
		if !&modifiable
			let b:statusline_long_line_warning = ''
			return b:statusline_long_line_warning
		endif
		let long_line_lens = s:LongLines()
		if len(long_line_lens) > 0
			let b:statusline_long_line_warning = "[" .
					\ '#' . len(long_line_lens) . "," .
					\ 'm' . s:Median(long_line_lens) . "," .
					\ '$' . max(long_line_lens) . "]"
		else
			let b:statusline_long_line_warning = ""
		endif
	endif
	return b:statusline_long_line_warning
endfunction

" return list containing lengths of long lines in this buffer
function s:LongLines()
	let threshold = (&tw ? &tw : 80)
	let spaces = repeat(" ", &ts)
	let long_line_lens = []
	let i = 1
	while i <= line("$")
		let len = strlen(substitute(getline(i), '\t', spaces, 'g'))
		if len > threshold
			call add(long_line_lens, len)
		endif
		let i += 1
	endwhile
	return long_line_lens
endfunction

function s:Median(nums)  " find the median of given array of numbers
	let nums = sort(a:nums)
	let l = len(nums)
	if l % 2 == 1
		let i = (l - 1) / 2
		return nums[i]
	else
		return nums[l / 2] + nums[(l / 2) - 1] / 2
	endif
endfunction

se laststatus=2
se statusline=%-03.f
se statusline+=%#warningmsg#
se statusline+=%{&ff!='unix'?'['.&ff.']':''}
se statusline+=%*
se statusline+=%#warningmsg#
se statusline+=%{(&fenc!='utf-8'&&&fenc!='')?'['.&fenc.']':''}
se statusline+=%*
se statusline+=%h%y%r%m
se statusline+=%{StatuslineTrailingSpaceWarning()}
se statusline+=%{StatuslineLongLineWarning()}
se statusline+=%#warningmsg#
se statusline+=%#error#
se statusline+=%{&paste?'[paste]':''}
se statusline+=%*
se statusline+=%{FugitiveStatusline()}
se statusline+=%=
se statusline+=%{StatuslineCurrentHighlight()}\ \
se statusline+=%c,
se statusline+=%l/%L
se statusline+=\ %P
autocmd cursorhold,bufwritepost * unlet! b:statusline_trailing_space_warning
autocmd cursorhold,bufwritepost * unlet! b:statusline_long_line_warning
augroup PreWriteEdits
autocmd BufWritePre * call PrepareBeforeWrite()
augroup END
syntax on
